import org.apache.tools.ant.filters.FixCrLfFilter

import java.nio.file.Files
import java.nio.file.Paths

apply plugin: 'com.android.library'
apply from: file(rootProject.file('module.gradle'))

android {
    namespace "zygisk.il2cppdumper"
    compileSdkVersion rootProject.ext.targetSdkVersion
    ndkVersion '28.2.13676358'

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        externalNativeBuild {
            cmake {
                arguments "-DMODULE_NAME:STRING=$moduleLibraryName"
            }
        }
    }

    buildFeatures {
        prefab true
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "4.1.2"
        }
    }
}

repositories {
    mavenLocal()
}

afterEvaluate {
    android {
        libraryVariants.forEach { variant ->
            def variantCapped = variant.name.capitalize()
            def variantLowered = variant.name.toLowerCase()

            def zipName = "${moduleName.replace(' ', '_')}-${moduleVersion}(${moduleVersionCode})-${variantLowered}.zip"
            def magiskDir = file("$outDir/magisk_module_$variantLowered")

            tasks.register("prepareMagiskFiles${variantCapped}", Sync) {
                dependsOn("assemble$variantCapped")

                def templatePath = "$rootDir/template/magisk_module"

                into magiskDir
                from(templatePath) {
                    exclude 'module.prop'
                }

                from(templatePath) {
                    include 'module.prop'
                    expand([
                            id         : magiskModuleId,
                            name       : moduleName,
                            version    : moduleVersion,
                            versionCode: moduleVersionCode.toString(),
                            author     : moduleAuthor,
                            description: moduleDescription,
                    ])
                    filter(FixCrLfFilter.class,
                            eol: FixCrLfFilter.CrLf.newInstance("lf"))
                }

                from(layout.buildDirectory.dir("intermediates/stripped_native_libs/$variantLowered/out/lib")) {
                    into 'lib'
                }

                doLast {
                    file("$magiskDir/zygisk").mkdir()
                    fileTree("$magiskDir/lib").visit { f ->
                        if (!f.directory) return
                        def srcPath = Paths.get("${f.file.absolutePath}/lib${moduleLibraryName}.so")
                        def dstPath = Paths.get("$magiskDir/zygisk/${f.path}.so")
                        Files.move(srcPath, dstPath)
                    }
                    new File("$magiskDir/lib").deleteDir()
                }
            }

            tasks.register("zip${variantCapped}", Zip) {
                dependsOn("prepareMagiskFiles${variantCapped}")
                from magiskDir
                archiveFileName.set(zipName)
                destinationDirectory.set(outDir)
            }

            tasks.register("push${variantCapped}", Exec) {
                dependsOn("zip${variantCapped}")
                workingDir outDir
                commandLine android.adbExecutable, "push", zipName, "/data/local/tmp/"
            }

            tasks.register("flash${variantCapped}", Exec) {
                dependsOn("push${variantCapped}")
                commandLine android.adbExecutable, "shell", "su", "-c",
                        "magisk --install-module /data/local/tmp/${zipName}"
            }

            tasks.register("flashAndReboot${variantCapped}", Exec) {
                dependsOn("flash${variantCapped}")
                commandLine android.adbExecutable, "shell", "reboot"
            }

            variant.assembleProvider.get().finalizedBy("zip${variantCapped}")
        }
    }
}